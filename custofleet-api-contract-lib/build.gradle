plugins {
    id 'org.openapi.generator' version '6.6.0'
    id 'java-library'
}

repositories {
    mavenCentral()
}


dependencies {
    compileOnly 'javax:javaee-api:8.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    
}

description = 'custofleet-api-contract-lib'

task cleanOutputDir(type: Delete) {
    delete "$projectDir/src/main/java/"
}

task generateApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    group = 'Custofleet customized task'
    description = 'Generate code for custofleet request'
    generatorName = "jaxrs-spec"
    inputSpec = "$projectDir/specs/custofleet.json".toString()
    outputDir = "$projectDir".toString()
    apiPackage = "com.it.ibm.custofleet.restservices.api"
    modelPackage = "com.it.ibm.custofleet.restservices.model"
    configOptions = [
        dateLibrary: 'java8',
        interfaceOnly: 'true',
        sourceFolder: 'src/main/java',
        useSwaggerAnnotations: 'false',
        hideGenerationTimestamp: 'true',
        returnResponse: 'true'
    ]
}

generateApi.dependsOn cleanOutputDir

openApiValidate {
    inputSpec = "$projectDir/specs/custofleet.json".toString()
}

compileJava.dependsOn generateApi
build.dependsOn generateApi
